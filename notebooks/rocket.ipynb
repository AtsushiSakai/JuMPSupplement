{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Rocket Control with JuMP\n",
    "\n",
    "*This problem was drawn from the [COPS3](http://www.mcs.anl.gov/~more/cops/cops3.pdf) benchmark, and the notebook is from the [JuliaOpt notebook collection](https://github.com/JuliaOpt/juliaopt-notebooks)*\n",
    "\n",
    "Our goal is to maximize the final altitude of a vertically launched rocket. We can control the thrust of the rocket, and must take account of the rocket mass, fuel consumption rate, gravity, and aerodynamic drag.\n",
    "\n",
    "Let us consider the basic description of the model (for the full description, including parameters for the rocket, see the COPS3 PDF)\n",
    "\n",
    "### Overview\n",
    "\n",
    "We will use a discretized model of time, with a fixed number of time steps, $n$. We will make the time step size $\\Delta t$, and thus the final time $t_f = n \\cdot \\Delta t$, a variable in the problem. To approximate the derivatives in the problem we will use the [trapezoidal rule](http://en.wikipedia.org/wiki/Trapezoidal_rule).\n",
    "\n",
    "### State and Control\n",
    "\n",
    "We will have three state variables:\n",
    "\n",
    "* Velocity, $v$\n",
    "* Altitude, $h$\n",
    "* Mass of rocket and remaining fuel, $m$\n",
    "\n",
    "and a single control variable, thrust $T$. Our goal is thus to maximize $h(t_f)$. Each of these corresponds to a JuMP variable indexed by the time step.\n",
    "\n",
    "### Dynamics\n",
    "\n",
    "We have three equations that control the dynamics of the rocket:\n",
    "\n",
    "Rate of ascent: $$h^\\prime = v$$\n",
    "Acceleration: $$v^\\prime = \\frac{T - D(h,v)}{m} - g(h)$$\n",
    "Rate of mass loss: $$m^\\prime = -\\frac{T}{c}$$\n",
    "\n",
    "where drag $D(h,v)$ is a function of altitude and velocity, and gravity $g(h)$ is a function of altitude. These forces are defined as\n",
    "\n",
    "$$D(h,v) = D_c v^2 exp\\left( -h_c \\left( \\frac{h-h(0)}{h(0)} \\right) \\right)$$\n",
    "and\n",
    "$$g(h) = g_0 \\left( \\frac{h(0)}{h} \\right)^2$$\n",
    "\n",
    "The three rate equations correspond to JuMP constraints, and for convenience we will represent the forces with nonlinear expressions that we define seperately with `@defNLExpr`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "using JuMP, Ipopt, Interact, Gadfly"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# Create JuMP model, using Ipopt as the solver\n",
    "mod = Model(solver=IpoptSolver(print_level=0))\n",
    "\n",
    "# Constants\n",
    "# Note that all parameters in the model have been normalized\n",
    "# to be dimensionless. See the COPS3 paper for more info.\n",
    "h_0 = 1    # Initial height\n",
    "v_0 = 0    # Initial velocity\n",
    "m_0 = 1    # Initial mass\n",
    "g_0 = 1    # Gravity at the surface\n",
    "\n",
    "# Parameters\n",
    "T_c = 3.5  # Used for thrust\n",
    "@defNLParam(mod, h_c == 500)  # Used for drag\n",
    "v_c = 620  # Used for drag\n",
    "m_c = 0.6  # Fraction of initial mass left at end\n",
    "\n",
    "# Derived parameters\n",
    "c     = 0.5*sqrt(g_0*h_0)  # Thrust-to-fuel mass\n",
    "m_f   = m_c*m_0            # Final mass\n",
    "D_c   = 0.5*v_c*m_0/g_0    # Drag scaling\n",
    "T_max = T_c*g_0*m_0        # Maximum thrust\n",
    "\n",
    "n = 100   # Time steps\n",
    "@defVar(mod, Δt ≥ 0, start = 1/n)   # Time step\n",
    "@defNLExpr(mod, t_f, Δt*n)          # Time of flight\n",
    "\n",
    "# State variables\n",
    "@defVar(mod, v[0:n] ≥ 0)            # Velocity\n",
    "@defVar(mod, h[0:n] ≥ h_0)          # Height\n",
    "@defVar(mod, m_f ≤ m[0:n] ≤ m_0)    # Mass\n",
    "\n",
    "# Control: thrust\n",
    "@defVar(mod, 0 ≤ T[0:n] ≤ T_max)\n",
    "\n",
    "# Objective: maximize altitude at end of time of flight\n",
    "@setObjective(mod, Max, h[n])\n",
    "\n",
    "# Initial conditions\n",
    "@addConstraint(mod, v[0] == v_0)\n",
    "@addConstraint(mod, h[0] == h_0)\n",
    "@addConstraint(mod, m[0] == m_0)\n",
    "@addConstraint(mod, m[n] == m_f)\n",
    "\n",
    "# Forces\n",
    "# Drag(h,v) = Dc v^2 exp( -hc * (h - h0) / h0 )\n",
    "@defNLExpr(mod, drag[j=0:n], D_c*(v[j]^2)*exp(-h_c*(h[j]-h_0)/h_0))\n",
    "# Grav(h)   = go * (h0 / h)^2\n",
    "@defNLExpr(mod, grav[j=0:n], g_0*(h_0/h[j])^2)\n",
    "\n",
    "# Dynamics\n",
    "for j in 1:n\n",
    "    # h' = v\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, h[j] == h[j-1] + Δt*v[j-1])\n",
    "    # Trapezoidal integration\n",
    "    @addNLConstraint(mod,\n",
    "        h[j] == h[j-1] + 0.5*Δt*(v[j]+v[j-1]))\n",
    "\n",
    "    # v' = (T-D(h,v))/m - g(h)\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, v[j] == v[j-1] + Δt*(\n",
    "    #                 (T[j-1] - drag[j-1])/m[j-1] - grav[j-1]))\n",
    "    # Trapezoidal integration\n",
    "    @addNLConstraint(mod,\n",
    "        v[j] == v[j-1] + 0.5*Δt*(\n",
    "            (T[j  ] - drag[j  ] - m[j  ]*grav[j  ])/m[j  ] +\n",
    "            (T[j-1] - drag[j-1] - m[j-1]*grav[j-1])/m[j-1] ))\n",
    "\n",
    "    # m' = -T/c\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, m[j] == m[j-1] - Δt*T[j-1]/c)\n",
    "    # Trapezoidal integration\n",
    "    @addNLConstraint(mod,\n",
    "        m[j] == m[j-1] - 0.5*Δt*(T[j] + T[j-1])/c)\n",
    "end\n",
    "\n",
    "# Provide starting solution\n",
    "for k in 0:n\n",
    "    setValue(h[k], 1)\n",
    "    setValue(v[k], (k/n)*(1 - (k/n)))\n",
    "    setValue(m[k], (m_f - m_0)*(k/n) + m_0)\n",
    "    setValue(T[k], T_max/2)\n",
    "end\n",
    "\n",
    "solve(mod)\n",
    "\n",
    "@manipulate for T_c in 1.0:0.5:6.0, new_h_c in 300:100:700\n",
    "    # Update parameters\n",
    "    setValue(h_c, new_h_c)\n",
    "    T_max = T_c*g_0*m_0\n",
    "    for k in 0:n\n",
    "        setUpper(T[k], T_max)\n",
    "    end\n",
    "\n",
    "    # Solve for the control and state\n",
    "    status = solve(mod)\n",
    "\n",
    "    # Display results\n",
    "    h_plot = plot(\n",
    "    x=(0:n)*getValue(Δt),\n",
    "    y=[getValue(h[i]) for i in 0:n],\n",
    "    Geom.line,\n",
    "    Scale.y_continuous(minvalue=1.0, maxvalue=1.02),\n",
    "    Scale.x_continuous(minvalue=0.0, maxvalue=0.25),\n",
    "    Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Altitude\"))\n",
    "    \n",
    "    m_plot = plot(\n",
    "    x=(0:n)*getValue(Δt),\n",
    "    y=[getValue(m[i]) for i in 0:n],\n",
    "    Geom.line,\n",
    "    Scale.y_continuous(minvalue=0.6, maxvalue=1.0),\n",
    "    Scale.x_continuous(minvalue=0.0, maxvalue=0.25),\n",
    "    Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Mass\"))\n",
    "    \n",
    "    v_plot = plot(\n",
    "    x=(0:n)*getValue(Δt),\n",
    "    y=[getValue(v[i]) for i in 0:n],\n",
    "    Geom.line,\n",
    "    Scale.y_continuous(minvalue=0.0, maxvalue=0.2),\n",
    "    Scale.x_continuous(minvalue=0.0, maxvalue=0.25),\n",
    "    Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Velocity\"))\n",
    "    \n",
    "    T_plot = plot(\n",
    "    x=(0:n)*getValue(Δt),\n",
    "    y=[getValue(T[i]) for i in 0:n],\n",
    "    Geom.line,\n",
    "    Scale.y_continuous(minvalue=0.0, maxvalue=6.0),\n",
    "    Scale.x_continuous(minvalue=0.0, maxvalue=0.25),\n",
    "    Guide.xlabel(\"Time (s)\"),\n",
    "    Guide.ylabel(\"Thrust\"))\n",
    "\n",
    "    vstack( hstack(h_plot,m_plot), hstack(v_plot,T_plot) )   \n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.4.0",
   "language": "julia",
   "name": "julia-0.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.4.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
